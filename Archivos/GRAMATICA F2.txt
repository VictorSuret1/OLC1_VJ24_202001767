<INICIO> ::= <INSTRUCCIONES>

<INSTRUCCIONES> ::= <INSTRUCCIONES> <INSTRUCCION>
                   | <INSTRUCCION>

<INSTRUCCION> ::= <IMPRESION>
                 | <DECLARACION>
                 | <ASIGNACION>
                 | <INCREMENTOYDECREMENTO>
                 | <SIF>
                 | <MATCH_SENTENCIA>
                 | <CFOR>
                 | <CWHILE>
                 | <CDOWHILE>
                 | <BREAKK>
                 | <CONTINUEE>
                 | <DECLARACIONVEC>
                 | <METODO>
                 | <STARTWITH>
                 | <RETORNO>
                 | <ASIGNACIONVEC>
                 | <DECLARACIONLISTA>
                 | <APPENDLISTA>
                 | <REMOVELISTA>
                 | <LLAMADA>
                 | <FUNCION>
                 | <DECLARACIONSTRUCT>
                 | <INSTANCIACIONSTRUCT>
                 | <ASIGNACIONSTRUCT>
                 | <F_LENGTH>
                 | error FINCADENA

<F_LENGTH> ::= LENGTH PAR1 <EXPRESION> PAR2 FINCADENA

<DECLARACIONSTRUCT> ::= STRUCT LLAVE1 <LISTA_STRUCT> LLAVE2 ID FINCADENA

<LISTA_STRUCT> ::= <LISTA_STRUCT> FINCADENA ID DOSP <TIPOS> FINCADENA
                  | ID DOSP <TIPOS>

<INSTANCIACIONSTRUCT> ::= <MUTABILIDAD> ID DOSP ID IGUAL LLAVE1 <VALORES_STRUCT> LLAVE2 FINCADENA

<VALORES_STRUCT> ::= <VALORES_STRUCT> COMA ID DOSP <EXPRESION>
                    | ID DOSP <EXPRESION>

<ASIGNACIONSTRUCT> ::= ID PUNTO ID IGUAL <EXPRESION> FINCADENA

<IMPRESION> ::= PRINTLN PAR1 <EXPRESION> PAR2 FINCADENA

<FUNCION> ::= <TIPOS> ID PAR1 <PARAMS> PAR2 LLAVE1 <INSTRUCCIONES> LLAVE2
             | <TIPOS> ID PAR1 PAR2 LLAVE1 <INSTRUCCIONES> LLAVE2

<RETORNO> ::= RETURN <EXPRESION> FINCADENA
             | RETURN FINCADENA

<LLAMADA> ::= ID PAR1 <PARAMSCALL> PAR2 FINCADENA
             | ID PAR1 PAR2 FINCADENA

<DECLARACIONLISTA> ::= LIST MENOR <TIPOS> MAYOR ID IGUAL NEW LIST PAR1 PAR2 FINCADENA

<APPENDLISTA> ::= ID PUNTO APPEND PAR1 <EXPRESION> PAR2 FINCADENA

<ASIGNACIONLISTA> ::= ID BRIZQ <EXPRESION> BRDER IGUAL <EXPRESION> FINCADENA

<REMOVELISTA> ::= ID PUNTO REMOVE PAR1 <EXPRESION> PAR2 FINCADENA

<DECLARACIONVEC> ::= <MUTABILIDAD> ID DOSP <TIPOS> BRIZQ BRDER IGUAL BRIZQ <LISTAVALORES> BRDER FINCADENA
                    | <MUTABILIDAD> ID DOSP <TIPOS> BRIZQ BRDER BRIZQ BRDER IGUAL BRIZQ <LISTAVALORES2D> BRDER FINCADENA

<LISTAVALORES> ::= <LISTAVALORES> COMA <EXPRESION>
                  | <EXPRESION>

<LISTAVALORES2D> ::= <LISTAVALORES2D> COMA BRIZQ <LISTAVALORES> BRDER
                    | BRIZQ <LISTAVALORES> BRDER

<ASIGNACIONVEC> ::= ID BRIZQ <EXPRESION> BRDER IGUAL <EXPRESION> FINCADENA
                   | ID BRIZQ <EXPRESION> BRDER BRIZQ <EXPRESION> BRDER IGUAL <EXPRESION> FINCADENA

<DECLARACION> ::= <MUTABILIDAD> ID DOSP <TIPOS> FINCADENA
                 | <MUTABILIDAD> ID DOSP <TIPOS> IGUAL <EXPRESION> FINCADENA

<ASIGNACION> ::= ID IGUAL <EXPRESION> FINCADENA

<TIPOS> ::= INT
           | DOUBLE
           | STRING
           | CHAR
           | BOOL
           | STR

<MUTABILIDAD> ::= CONST
                 | VAR

<TIPO_VOID> ::= VOID

<STARTWITH> ::= START ID PAR1 <PARAMSCALL> PAR2 FINCADENA
               | START ID PAR1 PAR2 FINCADENA

<METODO> ::= <TIPO_VOID> ID PAR1 <PARAMS> PAR2 LLAVE1 <INSTRUCCIONES> LLAVE2
            | <TIPO_VOID> ID PAR1 PAR2 LLAVE1 <INSTRUCCIONES> LLAVE2

<PARAMS> ::= <PARAMS> COMA <TIPOS> ID
            | <TIPOS> ID

<PARAMSCALL> ::= <PARAMSCALL> COMA <EXPRESION>
                | <EXPRESION>

<CASTEO> ::= PAR1 <TIPOS> PAR2 <EXPRESION> %prec PAR1

<INCREMENTOYDECREMENTO> ::= ID INCREMENTO FINCADENA
                           | ID DECREMENTO FINCADENA
                           | ID IGUAL <EXPRESION>

<SIF> ::= IF PAR1 <EXPRESION> PAR2 LLAVE1 <INSTRUCCIONES> LLAVE2
         | IF PAR1 <EXPRESION> PAR2 LLAVE1 <INSTRUCCIONES> LLAVE2 ELSE LLAVE1 <INSTRUCCIONES> LLAVE2
         | IF PAR1 <EXPRESION> PAR2 LLAVE1 <INSTRUCCIONES> LLAVE2 ELSEIF PAR1 <EXPRESION> PAR2 LLAVE1 <INSTRUCCIONES> LLAVE2

<MATCH_SENTENCIA> ::= MATCH <EXPRESION> LLAVE1 <CASES_LIST> LLAVE2
                     | MATCH <EXPRESION> LLAVE1 <CASES_LIST> FLECHA LLAVE1 <INSTRUCCIONES> LLAVE2 LLAVE2
                     | MATCH <EXPRESION> LLAVE1 <CASES_LIST> GUIONBAJO FLECHA LLAVE1 <INSTRUCCIONES> LLAVE2 LLAVE2

<CASES_LIST> ::= <CASE>
                | <CASES_LIST> <CASE>

<CASE> ::= <EXPRESION> FLECHA LLAVE1 <INSTRUCCIONES> LLAVE2
          | <EXPRESION> GUIONBAJO FLECHA LLAVE1 <INSTRUCCIONES> LLAVE2

<CFOR> ::= FOR PAR1 <ASIGNACION> <EXPRESION> FINCADENA <ACTUALIZA_FOR> PAR2 LLAVE1 <INSTRUCCIONES> LLAVE2

<ACTUALIZA_FOR> ::= ID IGUAL <EXPRESION>
                   | ID INCREMENTO
                   | ID DECREMENTO

<BREAKK> ::= BREAK FINCADENA

<CONTINUEE> ::= CONTINUE FINCADENA

<CWHILE> ::= WHILE PAR1 <EXPRESION> PAR2 LLAVE1 <INSTRUCCIONES> LLAVE2

<CDOWHILE> ::= DO LLAVE1 <INSTRUCCIONES> LLAVE2 WHILE PAR1 <EXPRESION> PAR2 FINCADENA

<EXPRESION> ::= <CASTEO>
                | MENOS <EXPRESION>
                | <EXPRESION> MAS <EXPRESION>
                | <EXPRESION> MENOS <EXPRESION>
                | <EXPRESION> MULTIPLICACION <EXPRESION>
                | <EXPRESION> DIVISION <EXPRESION>
                | <EXPRESION> MODULO <EXPRESION>
                | <EXPRESION> POTENCIA <EXPRESION>
                | <EXPRESION> EQUALS <EXPRESION>
                | <EXPRESION> NOTEQUALS <EXPRESION>
                | <EXPRESION> MAYOR <EXPRESION>
                | <EXPRESION> MAYORQUE <EXPRESION>
                | <EXPRESION> MENOR <EXPRESION>
                | <EXPRESION> MENORQUE <EXPRESION>
                | <EXPRESION> OR <EXPRESION>
                | <EXPRESION> AND <EXPRESION>
                | <EXPRESION> XOR <EXPRESION>
                | NOT <EXPRESION>
                | PAR1 <EXPRESION> PAR2
                | ENTERO
                | DECIMAL
                | CADENA
                | CARACTER
                | TRUE
                | FALSE
                | ID
                | ID BRIZQ <EXPRESION> BRDER
                | ID BRIZQ <EXPRESION> BRDER BRIZQ <EXPRESION> BRDER
                | ID PUNTO ID
                | <LLAMADA>
                | TOSTRING PAR1 <EXPRESION> PAR2
                | LENGTH PAR1 <EXPRESION> PAR2
                | ID PUNTO REMOVE PAR1 <EXPRESION> PAR2
                | ID PUNTO FIND PAR1 <EXPRESION> PAR2